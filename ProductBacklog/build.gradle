apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply from: 'javafx.plugin'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'nu.studer:gradle-jooq-plugin:1.0.5'
        classpath 'org.xerial:sqlite-jdbc:3.8.7' // database-specific JDBC driver
    }
    configurations.classpath {
        resolutionStrategy {                            // enforce a specific jOOQ version
            forcedModules = [
                'org.jooq:jooq:3.6.0',
                'org.jooq:jooq-meta:3.6.0',
                'org.jooq:jooq-codegen:3.6.0'
            ]     
        }
    }
}

apply plugin: 'nu.studer.jooq'

// Restricting to Java 7 is a workaround for a bug in cglib when creating a mock of ProductBacklogComparison
// See http://stackoverflow.com/questions/27092857/cglib-throws-an-illegalargumentexception-when-enhancing-the-java-util-date-class
sourceCompatibility = 1.7

version = '0.2.3'
mainClassName = 'de.frankbeeh.productbacklogtimeline.ProductBacklogTimelineApplication'

javafx {
    mainClass = 'de.frankbeeh.productbacklogtimeline.ProductBacklogTimelineApplication'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Product Backlog Timeline',
                   'Implementation-Version': version
    }
}

eclipse {
	project {
		natures 'org.eclipse.xtext.ui.shared.xtextNature'
		buildCommand 'org.eclipse.xtext.ui.shared.xtextBuilder'
	}
}

repositories {
    mavenCentral()
    maven {
        url "https://maven.atlassian.com/repository/public"
    }
    maven {
        url "http://oauth.googlecode.com/svn/code/maven/"
    }
}

dependencies {
    compile 'org.xerial:sqlite-jdbc:3.8.7'
    compile 'org.jooq:jooq:3.6.0'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'com.google.guava:guava:18.0'
    compile 'net.sf.opencsv:opencsv:2.3'
    compile 'com.atlassian.jira:jira-rest-java-client:0.5'

	// This is used by package com.atlassian.oauth.client.example
    compile 'net.oauth.core:oauth:20100527'
    compile 'net.oauth.core:oauth-consumer:20100527'
    compile 'net.oauth.core:oauth-httpclient4:20090913'

    testCompile 'junit:junit:4.+'
    testCompile 'org.easymock:easymock:3.3'
    testCompile "org.testfx:testfx-core:4.0.+"
    testCompile "org.testfx:testfx-junit:4.0.+"
}

jooq {
   main(sourceSets.main) {
       jdbc {
           driver = 'org.sqlite.JDBC'
           url = 'jdbc:sqlite:jooq/template.db'
       }
       generator {
           name = 'org.jooq.util.DefaultGenerator'
           strategy {
               name = 'org.jooq.util.DefaultGeneratorStrategy'
           }
           database {
               name = 'org.jooq.util.sqlite.SQLiteDatabase'
               dateAsTimestamp = false
           }
           generate {
               deprecated = false
               records = false
           }
           target {
               packageName = 'de.frankbeeh.productbacklogtimeline.service.database.generated'
           }
       }
   }
}
